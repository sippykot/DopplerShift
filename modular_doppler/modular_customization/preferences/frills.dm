/// Frills fixing
/obj/item/organ/frills
	name = "frills"

/datum/bodypart_overlay/mutant/frills
	layers = EXTERNAL_FRONT

/datum/bodypart_overlay/mutant/frills/color_images(list/image/overlays, layer, obj/item/bodypart/limb)
	draw_color = limb.owner?.dna.features[FEATURE_FRILLS_COLORS]
	return ..()

//core toggle
/datum/preference/toggle/frills
	savefile_key = "has_frills"
	savefile_identifier = PREFERENCE_CHARACTER
	category = PREFERENCE_CATEGORY_SECONDARY_FEATURES
	priority = PREFERENCE_PRIORITY_DEFAULT

/datum/preference/toggle/frills/apply_to_human(mob/living/carbon/human/target, value)
	if(value == FALSE)
		target.dna.features[FEATURE_FRILLS] = /datum/sprite_accessory/blank::name

/datum/preference/toggle/frills/create_default_value()
	return FALSE

/datum/preference/toggle/frills/is_accessible(datum/preferences/preferences)
	. = ..()
	var/datum/species/species = preferences.read_preference(/datum/preference/choiced/species)
	if(!species_can_access_mutant_customization(species))
		return FALSE
	return TRUE

/datum/preference/choiced/species_feature/lizard_frills
	feature_key = FEATURE_FRILLS

/datum/preference/choiced/species_feature/lizard_frills/icon_for(value)
	var/datum/sprite_accessory/sprite_accessory = get_accessory_for_value(value)
	var/static/datum/universal_icon/body
	if (isnull(body))
		body = uni_icon('icons/mob/human/species/lizard/bodyparts.dmi', "lizard_head")
	var/datum/universal_icon/final_icon = body.copy()

	if(sprite_accessory.icon_state != "none")
		if(icon_exists(sprite_accessory.icon, "m_frills_[sprite_accessory.icon_state]_ADJ"))
			var/datum/universal_icon/accessory_icon = uni_icon(sprite_accessory.icon, "m_frills_[sprite_accessory.icon_state]_ADJ")
			accessory_icon.shift(NORTH, 0)
			accessory_icon.blend_color(COLOR_WEBSAFE_DARK_GRAY, ICON_MULTIPLY)
			final_icon.blend_icon(accessory_icon, ICON_OVERLAY)
		if(icon_exists(sprite_accessory.icon, "m_frills_[sprite_accessory.icon_state]_FRONT"))
			var/datum/universal_icon/accessory_icon = uni_icon(sprite_accessory.icon, "m_frills_[sprite_accessory.icon_state]_FRONT")
			accessory_icon.shift(NORTH, 0)
			accessory_icon.blend_color(COLOR_WEBSAFE_DARK_GRAY, ICON_MULTIPLY)
			final_icon.blend_icon(accessory_icon, ICON_OVERLAY)

	final_icon.crop(11, 20, 23, 32)
	final_icon.scale(32, 32)

	return final_icon

/datum/species/regenerate_organs(mob/living/carbon/target, datum/species/old_species, replace_current = TRUE, list/excluded_zones, visual_only = FALSE, replace_missing = TRUE)
	. = ..()
	if(target.dna.features["frills"] && can_regenerate_mutant_feature("frills"))
		if(target.dna.features[FEATURE_FRILLS] != /datum/sprite_accessory/blank::name)
			var/obj/item/organ/replacement = SSwardrobe.provide_type(/obj/item/organ/frills)
			replacement.Insert(target, special = TRUE, movement_flags = DELETE_IF_REPLACED)
			return .
	var/obj/item/organ/old_part = target.get_organ_slot(ORGAN_SLOT_EXTERNAL_FRILLS)
	if(old_part)
		old_part.Remove(target, special = TRUE, movement_flags = DELETE_IF_REPLACED)
		old_part.moveToNullspace()

//sprite selection
/datum/preference/choiced/species_feature/lizard_frills
	category = PREFERENCE_CATEGORY_CLOTHING

/datum/preference/choiced/species_feature/lizard_frills/is_accessible(datum/preferences/preferences)
	. = ..()
	var/datum/species/species = preferences.read_preference(/datum/preference/choiced/species)
	if(!species_can_access_mutant_customization(species))
		return FALSE
	var/has_frills = preferences.read_preference(/datum/preference/toggle/frills)
	if(has_frills == TRUE)
		return TRUE
	return FALSE

/datum/preference/choiced/species_feature/lizard_frills/create_default_value()
	return /datum/sprite_accessory/blank::name
