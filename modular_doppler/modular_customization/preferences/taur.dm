/datum/species/get_features()
	var/list/features = ..()

	features += /datum/preference/choiced/species_feature/taur_type

	GLOB.features_by_species[type] = features

	return features

// dna is a string
/datum/species/regenerate_organs(mob/living/carbon/target, datum/species/old_species, replace_current = TRUE, list/excluded_zones, visual_only = FALSE, replace_missing = TRUE)
	. = ..()
	if(target.dna.features[FEATURE_TAUR] && can_regenerate_mutant_feature(FEATURE_TAUR))
		if(target.dna.features[FEATURE_TAUR] != /datum/sprite_accessory/blank::name)
			var/obj/item/organ/taur_body/body_to_use = /obj/item/organ/taur_body
			var/datum/sprite_accessory/taur/accessory = SSaccessories.feature_list[FEATURE_TAUR][target.dna.features[FEATURE_TAUR]]
			if (accessory)
				body_to_use = accessory.organ_type
			var/obj/item/organ/replacement  = SSwardrobe.provide_type(body_to_use)
			replacement.Insert(target, special = TRUE, movement_flags = DELETE_IF_REPLACED)
			return .
	var/obj/item/organ/old_part = target.get_organ_slot(ORGAN_SLOT_EXTERNAL_TAUR)
	if(old_part)
		old_part.Remove(target, special = TRUE, movement_flags = DELETE_IF_REPLACED)
		old_part.moveToNullspace()

//core toggle
/datum/preference/toggle/taur
	savefile_key = "has_taur"
	savefile_identifier = PREFERENCE_CHARACTER
	category = PREFERENCE_CATEGORY_SECONDARY_FEATURES
	priority = PREFERENCE_PRIORITY_DEFAULT

/datum/preference/toggle/taur/apply_to_human(mob/living/carbon/human/target, value)
	if(value == FALSE)
		target.dna.features[FEATURE_TAUR] = /datum/sprite_accessory/blank::name

/datum/preference/toggle/taur/create_default_value()
	return FALSE

/datum/preference/toggle/taur/is_accessible(datum/preferences/preferences)
	. = ..()
	var/species = preferences.read_preference(/datum/preference/choiced/species)
	if (!species_can_access_mutant_customization(species))
		return FALSE
	return TRUE

/datum/preference/choiced/species_feature/taur_type
	savefile_key = "feature_taur"
	main_feature_name = "Taur"
	savefile_identifier = PREFERENCE_CHARACTER
	category = PREFERENCE_CATEGORY_CLOTHING
	priority = PREFERENCE_PRIORITY_DEFAULT
	should_generate_icons = TRUE
	can_randomize = FALSE
	feature_key = FEATURE_TAUR

/datum/preference/choiced/species_feature/taur_type/icon_for(value)
	var/datum/sprite_accessory/taur/taur_acc = get_accessory_for_value(value)
	// TO THOSE RESEARCHING THIS CODE LATER! This initial blank sprite is ESSENTIAL. It allows to sprite to generate even if the initial ADJ sprite is broken or nonexistant.
	var/datum/universal_icon/final_icon = uni_icon('modular_doppler/taurs/icons/taur.dmi', "m_taur_none_ADJ", EAST)
	var/datum/universal_icon/accessory_icon = uni_icon(taur_acc.icon, "m_taur_[taur_acc.icon_state]_[taur_acc.primary_layer]", EAST)
	accessory_icon.blend_color(COLOR_RED, ICON_MULTIPLY)
	var/datum/universal_icon/accessory_icon_2 = null
	if (icon_exists(taur_acc.icon, "m_taur_[taur_acc.icon_state]_[taur_acc.primary_layer]_2"))
		accessory_icon_2 = uni_icon(taur_acc.icon, "m_taur_[taur_acc.icon_state]_[taur_acc.primary_layer]_2", EAST)
		accessory_icon_2.blend_color(COLOR_VIBRANT_LIME, ICON_MULTIPLY)
	var/datum/universal_icon/accessory_icon_3 = null
	if (icon_exists(taur_acc.icon, "m_taur_[taur_acc.icon_state]_[taur_acc.primary_layer]_3"))
		accessory_icon_3 = uni_icon(taur_acc.icon, "m_taur_[taur_acc.icon_state]_[taur_acc.primary_layer]_3", EAST)
		accessory_icon_3.blend_color(COLOR_BLUE, ICON_MULTIPLY)
	final_icon.blend_icon(accessory_icon, ICON_OVERLAY)
	if (istype(accessory_icon_2))
		final_icon.blend_icon(accessory_icon_2, ICON_OVERLAY)
	if (istype(accessory_icon_3))
		final_icon.blend_icon(accessory_icon_3, ICON_OVERLAY)

	final_icon.scale(64, 32)
	final_icon.shift(EAST, 0)


	return final_icon

/datum/preference/choiced/species_feature/taur_type/apply_to_human(mob/living/carbon/human/target, value)
	target.dna.features[FEATURE_TAUR] = value

/datum/preference/choiced/species_feature/taur_type/is_accessible(datum/preferences/preferences)
	. = ..()
	if (!.)
		return FALSE

	var/datum/species/species = preferences.read_preference(/datum/preference/choiced/species)
	if (!species_can_access_mutant_customization(species))
		return FALSE
	var/has_taur = preferences.read_preference(/datum/preference/toggle/taur)
	if(has_taur == TRUE)
		return TRUE
	return FALSE

/datum/preference/choiced/species_feature/taur_type/create_default_value()
	return /datum/sprite_accessory/taur/none::name

/datum/bodypart_overlay/mutant/taur_body
	layers = EXTERNAL_FRONT | EXTERNAL_ADJACENT | EXTERNAL_BEHIND | EXTERNAL_BODY_FRONT_UNDER_CLOTHES

	feature_key = FEATURE_TAUR
	feature_key_sprite = FEATURE_TAUR

/datum/bodypart_overlay/mutant/taur_body/color_images(list/image/overlays, layer, obj/item/bodypart/limb)
	draw_color = limb?.owner?.dna.features[FEATURE_TAUR_COLORS] // Taur bodies sometimes don't have an attached limb when getting deleted I think ?
	return ..()

/datum/bodypart_overlay/mutant/taur_body/can_draw_on_bodypart(obj/item/bodypart/bodypart_owner)
	. = ..()

	if (!.)
		return .

	var/obj/item/organ/taur_body/body = bodypart_owner
	if (istype(body))
		if (body.hide_self)
			return FALSE

		var/mob/living/carbon/human/owner = bodypart_owner.owner
		if(!istype(owner))
			return TRUE

		var/obj/item/clothing/suit/worn_suit = owner.wear_suit
		if (istype(worn_suit))
			if((worn_suit.flags_inv & HIDETAIL) && !worn_suit.gets_cropped_on_taurs)
				return TRUE

			if (worn_suit.flags_inv & HIDETAURIFCOMPATIBLE)
				for(var/shape in worn_suit.supported_bodyshapes)
					if(body.external_bodyshapes & shape)
						return FALSE
