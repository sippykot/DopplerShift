/datum/dna
	///	This variable is read by the regenerate_organs() proc to know what organ subtype to give
	var/wing_type = NO_VARIATION

/datum/species/regenerate_organs(mob/living/carbon/target, datum/species/old_species, replace_current = TRUE, list/excluded_zones, visual_only = FALSE, replace_missing = TRUE)
	. = ..()
	if(target == null)
		return
	if(!ishuman(target))
		return

	if(target.dna.features[FEATURE_MOTH_WINGS] && can_regenerate_mutant_feature(FEATURE_MOTH_WINGS))
		if(target.dna.wing_type == NO_VARIATION)
			return .
		if(target.dna.features["moth_wings"] != /datum/sprite_accessory/blank::name)
			var/obj/item/organ/replacement = SSwardrobe.provide_type(/obj/item/organ/wings/moth)
			replacement.Insert(target, special = TRUE, movement_flags = DELETE_IF_REPLACED)
			return .
	if(target.dna.features[FEATURE_WINGS] && can_regenerate_mutant_feature(FEATURE_WINGS))
		if(target.dna.features["wings"] != /datum/sprite_accessory/blank::name)
			var/obj/item/organ/replacement = SSwardrobe.provide_type(/obj/item/organ/wings/more)
			replacement.Insert(target, special = TRUE, movement_flags = DELETE_IF_REPLACED)
			return .

	var/obj/item/organ/wings/old_part = target.get_organ_slot(ORGAN_SLOT_EXTERNAL_WINGS)
	if(istype(old_part))
		old_part.Remove(target, special = TRUE, movement_flags = DELETE_IF_REPLACED)
		old_part.moveToNullspace()


/// Dropdown to select which ears you'll be rocking
/datum/preference/choiced/wing_variation
	savefile_key = "wing_type"
	savefile_identifier = PREFERENCE_CHARACTER
	category = PREFERENCE_CATEGORY_SECONDARY_FEATURES
	priority = PREFERENCE_PRIORITY_DEFAULT

/datum/preference/choiced/wing_variation/create_default_value()
	return NO_VARIATION

/datum/preference/choiced/wing_variation/init_possible_values()
	return list(NO_VARIATION, "Wings", "Moth Wings")

/datum/preference/choiced/wing_variation/apply_to_human(mob/living/carbon/human/target, chosen_variation)
	target.dna.wing_type = chosen_variation
	switch(chosen_variation)
		if(NO_VARIATION)
			target.dna.features[FEATURE_WINGS] = /datum/sprite_accessory/blank::name
			target.dna.features[FEATURE_MOTH_WINGS] = /datum/sprite_accessory/blank::name
		if("Wings")
			target.dna.features[FEATURE_MOTH_WINGS] = /datum/sprite_accessory/blank::name
		if("Moth Wings")
			target.dna.features[FEATURE_WINGS] = /datum/sprite_accessory/blank::name

/datum/preference/choiced/wing_variation/is_accessible(datum/preferences/preferences)
	. = ..()
	var/species = preferences.read_preference(/datum/preference/choiced/species)
	if (!species_can_access_mutant_customization(species))
		return FALSE
	return TRUE

//	Wings
/datum/preference/choiced/species_feature/wings
	savefile_key = "feature_wings"
	savefile_identifier = PREFERENCE_CHARACTER
	category = PREFERENCE_CATEGORY_CLOTHING
	should_generate_icons = TRUE
	main_feature_name = "Wings"
	feature_key = FEATURE_WINGS

/datum/preference/choiced/species_feature/wings/is_accessible(datum/preferences/preferences)
	. = ..()
	var/datum/species/species = preferences.read_preference(/datum/preference/choiced/species)
	if (!species_can_access_mutant_customization(species))
		return FALSE
	var/chosen_variation = preferences.read_preference(/datum/preference/choiced/wing_variation)
	if(chosen_variation == "Wings")
		return TRUE
	return FALSE

/datum/preference/choiced/species_feature/wings/create_default_value()
	return /datum/sprite_accessory/blank::name

/datum/preference/choiced/species_feature/wings/apply_to_human(mob/living/carbon/human/target, value)
	target.dna.features[FEATURE_WINGS] = value

/datum/preference/choiced/species_feature/wings/icon_for(value)
	var/datum/sprite_accessory/wings = get_accessory_for_value(value)
	return generate_back_icon(wings, FEATURE_WINGS)

/datum/preference/choiced/species_feature/moth_wings/icon_for(value)
	var/datum/sprite_accessory/wings = get_accessory_for_value(value)
	return generate_back_icon(wings, FEATURE_MOTH_WINGS)

//	Moth Wings
/datum/preference/choiced/species_feature/moth_wings
	category = PREFERENCE_CATEGORY_CLOTHING

/datum/preference/choiced/species_feature/moth_wings/is_accessible(datum/preferences/preferences)
	. = ..()
	var/datum/species/species = preferences.read_preference(/datum/preference/choiced/species)
	if (!species_can_access_mutant_customization(species))
		return FALSE
	var/chosen_variation = preferences.read_preference(/datum/preference/choiced/wing_variation)
	if(chosen_variation == "Moth Wings")
		return TRUE
	return FALSE

/datum/preference/choiced/species_feature/moth_wings/create_default_value()
	return /datum/sprite_accessory/blank::name

/// Overwrite lives here
//	This is for the triple color channel
/datum/bodypart_overlay/mutant/wings/more
	layers = EXTERNAL_FRONT | EXTERNAL_ADJACENT | EXTERNAL_BEHIND
	feature_key_sprite = "wings"

/datum/bodypart_overlay/mutant/wings/more/color_images(list/image/overlays, layer, obj/item/bodypart/limb)
	if(!limb) // I've seen runtimes you wouldn't believe
		return ..()
	if(!limb.owner)
		return ..()
	draw_color = limb.owner.dna.features[FEATURE_WINGS_COLORS]
	return ..()

/// If moth wings ever become 3 color like regular wings, remove this part and the color part below
/datum/bodypart_overlay/mutant/wings/moth/get_overlay(layer, obj/item/bodypart/limb)
	layer = bitflag_to_layer(layer)
	var/image/main_image = get_image(layer, limb)
	if(limb)
		main_image.alpha = limb.alpha
	color_image(main_image, layer, limb)
	if(blocks_emissive == EMISSIVE_BLOCK_NONE || !limb)
		return main_image

	var/list/all_images = list(
		main_image,
		emissive_blocker(main_image.icon, main_image.icon_state, limb, layer = main_image.layer, alpha = main_image.alpha)
	)
	return all_images

/// Overwrite lives here
//	Moth wings have their own bespoke RGB code.
/datum/bodypart_overlay/mutant/wings/moth/color_image(image/overlay, draw_layer, obj/item/bodypart/limb)
	if(limb == null)
		return ..()
	if(limb.owner == null)
		return ..()
	var/color_intended = COLOR_WHITE
	if(!length(limb.owner.dna.features[FEATURE_WINGS_COLORS]))
		return ..()
	var/tcol_1 = limb.owner.dna.features[FEATURE_WINGS_COLORS][1]
	var/tcol_2 = limb.owner.dna.features[FEATURE_WINGS_COLORS][2]
	var/tcol_3 = limb.owner.dna.features[FEATURE_WINGS_COLORS][3]
	if(tcol_1 && tcol_2 && tcol_3)
		//this is beyond ugly but it works
		var/r1 = hex2num(copytext(tcol_1, 2, 4)) / 255.0
		var/g1 = hex2num(copytext(tcol_1, 4, 6)) / 255.0
		var/b1 = hex2num(copytext(tcol_1, 6, 8)) / 255.0
		var/r2 = hex2num(copytext(tcol_2, 2, 4)) / 255.0
		var/g2 = hex2num(copytext(tcol_2, 4, 6)) / 255.0
		var/b2 = hex2num(copytext(tcol_2, 6, 8)) / 255.0
		var/r3 = hex2num(copytext(tcol_3, 2, 4)) / 255.0
		var/g3 = hex2num(copytext(tcol_3, 4, 6)) / 255.0
		var/b3 = hex2num(copytext(tcol_3, 6, 8)) / 255.0
		color_intended = list(r1,g1,b1, r2,g2,b2, r3,g3,b3)
	overlay.color = color_intended
	return overlay
