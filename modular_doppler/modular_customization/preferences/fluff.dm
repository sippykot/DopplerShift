/datum/species/regenerate_organs(mob/living/carbon/target, datum/species/old_species, replace_current = TRUE, list/excluded_zones, visual_only = FALSE, replace_missing = TRUE)
	. = ..()
	if(target.dna.features[FEATURE_FLUFF] && can_regenerate_mutant_feature(FEATURE_FLUFF))
		if(target.dna.features[FEATURE_FLUFF] != /datum/sprite_accessory/blank::name)
			var/obj/item/organ/replacement = SSwardrobe.provide_type(/obj/item/organ/fluff)
			replacement.Insert(target, special = TRUE, movement_flags = DELETE_IF_REPLACED)
			return .
	var/obj/item/organ/old_part = target.get_organ_slot(ORGAN_SLOT_EXTERNAL_FLUFF)
	if(old_part)
		old_part.Remove(target, special = TRUE, movement_flags = DELETE_IF_REPLACED)
		old_part.moveToNullspace()

//core toggle
/datum/preference/toggle/fluff
	savefile_key = "has_fluff"
	savefile_identifier = PREFERENCE_CHARACTER
	category = PREFERENCE_CATEGORY_SECONDARY_FEATURES
	priority = PREFERENCE_PRIORITY_DEFAULT

/datum/preference/toggle/fluff/apply_to_human(mob/living/carbon/human/target, value)
	if(value == FALSE)
		target.dna.features[FEATURE_FLUFF] = /datum/sprite_accessory/blank::name

/datum/preference/toggle/fluff/create_default_value()
	return FALSE

/datum/preference/toggle/fluff/is_accessible(datum/preferences/preferences)
	. = ..()
	var/species = preferences.read_preference(/datum/preference/choiced/species)
	if (!species_can_access_mutant_customization(species))
		return FALSE
	return TRUE

/datum/preference/choiced/species_feature/fluff
	savefile_key = "fluff"
	savefile_identifier = PREFERENCE_CHARACTER
	category = PREFERENCE_CATEGORY_CLOTHING
	should_generate_icons = TRUE
	main_feature_name = "Fluff"
	feature_key = FEATURE_FLUFF

/datum/preference/choiced/species_feature/fluff/is_accessible(datum/preferences/preferences)
	. = ..()
	var/datum/species/species = preferences.read_preference(/datum/preference/choiced/species)
	if(!species_can_access_mutant_customization(species))
		return FALSE
	var/has_fluff = preferences.read_preference(/datum/preference/toggle/fluff)
	if(has_fluff)
		return TRUE
	return FALSE

/datum/preference/choiced/species_feature/fluff/create_default_value()
	return /datum/sprite_accessory/blank::name

/datum/preference/choiced/species_feature/fluff/apply_to_human(mob/living/carbon/human/target, value)
	target.dna.features[FEATURE_FLUFF] = value

/datum/preference/choiced/species_feature/fluff/icon_for(value)
	var/static/datum/universal_icon/body
	if (isnull(body))
		body = uni_icon('icons/mob/human/bodyparts_greyscale.dmi', "human_head_f")
		body.blend_icon(uni_icon('icons/mob/human/bodyparts_greyscale.dmi', "human_chest_f"), ICON_OVERLAY)

	var/datum/universal_icon/final_icon = body.copy()
	if (value != SPRITE_ACCESSORY_NONE)
		var/datum/sprite_accessory/sprite_accessory = get_accessory_for_value(value)
		if(icon_exists(sprite_accessory.icon, "m_fluff_[sprite_accessory.icon_state]_ADJ"))
			var/datum/universal_icon/fluff_adj = uni_icon(sprite_accessory.icon, "m_fluff_[sprite_accessory.icon_state]_ADJ")
			fluff_adj.blend_color(COLOR_WEBSAFE_DARK_GRAY, ICON_MULTIPLY)
			final_icon.blend_icon(fluff_adj, ICON_OVERLAY)
		if(icon_exists(sprite_accessory.icon, "m_fluff_[sprite_accessory.icon_state]_FRONT"))
			var/datum/universal_icon/fluff_front = uni_icon(sprite_accessory.icon, "m_fluff_[sprite_accessory.icon_state]_FRONT")
			fluff_front.blend_color(COLOR_WEBSAFE_DARK_GRAY, ICON_MULTIPLY)
			final_icon.blend_icon(fluff_front, ICON_OVERLAY)

	final_icon.crop(10, 18, 22, 30)
	final_icon.scale(32, 32)

	return final_icon

/// Overwrite lives here
//	This is for the triple color channel
/datum/bodypart_overlay/mutant/fluff
	layers = EXTERNAL_FRONT | EXTERNAL_ADJACENT
	feature_key_sprite = FEATURE_FLUFF

/datum/bodypart_overlay/mutant/fluff/color_images(list/image/overlays, layer, obj/item/bodypart/limb)
	draw_color = limb.owner?.dna.features[FEATURE_FLUFF_COLORS]
	return ..()
